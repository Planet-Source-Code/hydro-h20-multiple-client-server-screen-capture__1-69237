VERSION 5.00
Begin VB.PropertyPage ppGIFpreview 
   Caption         =   "GIF Overview"
   ClientHeight    =   2400
   ClientLeft      =   0
   ClientTop       =   0
   ClientWidth     =   5565
   PaletteMode     =   0  'Halftone
   ScaleHeight     =   160
   ScaleMode       =   3  'Pixel
   ScaleWidth      =   371
   Begin Server.ucGIF ucGIF1 
      Height          =   1935
      Left            =   0
      TabIndex        =   10
      Top             =   0
      Width           =   3375
      _ExtentX        =   5953
      _ExtentY        =   3413
      BeginProperty Font {0BE35203-8F91-11CE-9DE3-00AA004BB851} 
         Name            =   "MS Sans Serif"
         Size            =   8.25
         Charset         =   0
         Weight          =   400
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
      fileLen         =   0
   End
   Begin VB.CommandButton cmdAccel 
      Caption         =   ">"
      Enabled         =   0   'False
      Height          =   390
      Index           =   1
      Left            =   5280
      TabIndex        =   7
      ToolTipText     =   "Speed Animation"
      Top             =   1980
      Visible         =   0   'False
      Width           =   240
   End
   Begin VB.CommandButton cmdAccel 
      Caption         =   "<"
      Enabled         =   0   'False
      Height          =   390
      Index           =   0
      Left            =   5055
      TabIndex        =   6
      ToolTipText     =   "Slow Animation"
      Top             =   1980
      Visible         =   0   'False
      Width           =   225
   End
   Begin VB.CommandButton cmdOpen 
      Caption         =   "Browse for File"
      Height          =   420
      Left            =   3480
      TabIndex        =   1
      Top             =   15
      Width           =   2040
   End
   Begin VB.CommandButton cmdAnimate 
      Caption         =   "&Animate File"
      Enabled         =   0   'False
      Height          =   390
      Left            =   3480
      TabIndex        =   0
      Top             =   1980
      Visible         =   0   'False
      Width           =   1575
   End
   Begin VB.Label Label1 
      Height          =   615
      Left            =   3480
      TabIndex        =   3
      Top             =   735
      Width           =   2040
   End
   Begin VB.Label lblLoops 
      Height          =   255
      Left            =   3480
      TabIndex        =   5
      Top             =   1755
      Width           =   2010
   End
   Begin VB.Label lblFile 
      Alignment       =   2  'Center
      Caption         =   "No File Selected"
      BeginProperty Font 
         Name            =   "Tahoma"
         Size            =   8.25
         Charset         =   0
         Weight          =   700
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
      ForeColor       =   &H8000000D&
      Height          =   285
      Left            =   30
      TabIndex        =   2
      Top             =   2010
      Width           =   3405
   End
   Begin VB.Label lblFrames 
      Caption         =   "Quick Look Statistics"
      Height          =   270
      Left            =   3480
      TabIndex        =   4
      Top             =   510
      Width           =   2055
   End
   Begin VB.Label lblTime 
      Height          =   225
      Left            =   3480
      TabIndex        =   9
      Top             =   1560
      Width           =   2025
   End
   Begin VB.Label lblReduced 
      ForeColor       =   &H000000C0&
      Height          =   225
      Left            =   3480
      TabIndex        =   8
      Top             =   1350
      Width           =   2025
   End
End
Attribute VB_Name = "ppGIFpreview"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False

Option Explicit
Private bDlgActive As Boolean
Private Declare Sub CopyMemory Lib "kernel32.dll" Alias "RtlMoveMemory" (ByRef Destination As Any, ByRef Source As Any, ByVal Length As Long)

Private Type OPENFILENAME
     lStructSize As Long
     hwndOwner As Long
     hInstance As Long
     lpstrFilter As String
     lpstrCustomFilter As String
     nMaxCustFilter As Long
     nFilterIndex As Long
     lpstrFile As String
     nMaxFile As Long
     lpstrFileTitle As String
     nMaxFileTitle As Long
     lpstrInitialDir As String
     lpstrTitle As String
     flags As Long
     nFileOffset As Integer
     nFileExtension As Integer
     lpstrDefExt As String
     lCustData As Long
     lpfnHook As Long
     lpTemplateName As String
End Type
Private Declare Function GetOpenFileName Lib "comdlg32.dll" Alias "GetOpenFileNameA" (pOpenfilename As OPENFILENAME) As Long
Private Const ucWidth As Long = 227
Private Const ucHeight As Long = 121
Private Const ucTop As Long = 1
Private Const ucLeft As Long = 1


Private Sub cmdAccel_Click(Index As Integer)
Dim frameNr As Long
On Error Resume Next    ' in case user exceeds Integer delay value while slowing down frames
For frameNr = 1 To ucGIF1.gifFrameCount
    With ucGIF1.Frame(frameNr)
        If Index = 0 Then ' slow down animation
            .frameDelay = .frameDelay + 50
        Else
            .frameDelay = .frameDelay - 50
        End If
    End With
Next

End Sub

Private Sub cmdAnimate_Click()
If cmdAnimate.Caption = "&Animate File" Then
    cmdAnimate.Caption = "Stop &Animation"
    ucGIF1.Animate = True
Else
    ucGIF1.Animate = False
    cmdAnimate.Caption = "&Animate File"
    lblFrames.Caption = "Number Frames: " & ucGIF1.gifFrameCount
    If ucGIF1.gifMaxLoops Then lblLoops = "Max Loops: " & Abs(ucGIF1.gifMaxLoops)
End If

End Sub

Private Sub cmdOpen_Click()
If GetFileName("") Then PropertyPage.Changed = True
End Sub

Private Sub PropertyPage_ApplyChanges()
Dim ucActive As ucGIF, ucFileName As String
Set ucActive = SelectedControls(0)
ucActive.LoadGIF lblFile.Tag
Set ucActive = Nothing
End Sub

Private Sub PropertyPage_EditProperty(PropertyName As String)

If PropertyName = "gifFileName" Then
    PropertyPage.Changed = False
    
    Dim ucActive As ucGIF, ucFileName As String
    Set ucActive = SelectedControls(0)
    ucFileName = ucActive.gifFileName
    Set ucActive = Nothing
    If ucFileName = "{none}" Then
        lblFile.Caption = "No File Selected"
    Else
        GetFileName ucFileName
    End If
End If
End Sub

Private Function GetFileName(fileName As String) As Boolean

On Error GoTo ExitRoutine:

bDlgActive = True

Dim fileTitle As String
Dim aniTime As Long, aniNr As Long, frameDelay As Integer
Dim resizeCx As Long, resizeCy As Long, bShrinkFor100Pct As Boolean
Dim sDetails As String

If Len(fileName) Then
    fileTitle = "From Design View User Control"
Else
    fileName = ShowBrowser(fileTitle)
    If fileName = "" Then GoTo ExitRoutine
    lblFile.Tag = fileName
End If

With ucGIF1

    .Visible = False
    If .Animate Then Call cmdAnimate_Click
    .LoadGIF fileName

    lblFrames.Caption = "Number Frames: " & .gifFrameCount
    Select Case .gifTransparencyState
    Case NotTransparent
        sDetails = "Transparent: No" & vbNewLine
    Case AllTransaprent
        sDetails = "Transparent: Yes" & vbNewLine
    Case PartialTransparent
        sDetails = "Transparent: Mixed" & vbNewLine
    End Select

    sDetails = sDetails & "Suggested Width: " & .gifLogicalWindowCx & vbNewLine
    sDetails = sDetails & "Suggested Height: " & .gifLogicalWindowCy & vbNewLine
    
    resizeCx = ucWidth
    resizeCy = ucHeight
    bShrinkFor100Pct = True
    .ScaleLogWinToSize resizeCx, resizeCy, bShrinkFor100Pct

    If bShrinkFor100Pct Then  ' the gif will fit in our max window size
        resizeCx = .gifLogicalWindowCx
        resizeCy = .gifLogicalWindowCy
        .Stretch = NoResizeFrames
        lblReduced.Caption = "Sample: Actual Size"
        lblReduced.ForeColor = vbBlue
    Else            ' gif is too big, need to scale down
        .AutoSize = False
        .Stretch = ScaleFrames
        ' light generally looks better, depending on how much is being scaled down
        .StretchMethod = scaleLight
        lblReduced.Caption = "Size Reduced to Fit Sample"
        lblReduced.ForeColor = vbRed
    End If
    .Move (ucWidth - resizeCx + .BorderWidth) \ 2 + ucLeft, (ucHeight - resizeCy + .BorderHeight) \ 2 + ucTop, resizeCx + .BorderHeight, resizeCy + .BorderHeight

    PropertyPage.Refresh

    If .gifFrameCount > 1 Then
        lblTime.Caption = "Length: " & Format$(.gifAnimationTime / 1000, "0.00") & " secs"
        If .gifMaxLoops = 0 Then
            lblLoops.Caption = "Max Loops: Infinite"
        Else
            lblLoops.Caption = "Max Loops: " & Abs(.gifMaxLoops)
        End If
        cmdAnimate.Enabled = True
    Else
        lblTime.Caption = ""
        lblLoops.Caption = ""
        cmdAnimate.Enabled = False
    End If

    .Visible = True

End With

cmdAnimate.Visible = cmdAnimate.Enabled
cmdAccel(0).Visible = cmdAnimate.Enabled
cmdAccel(1).Visible = cmdAnimate.Enabled
cmdAccel(0).Enabled = cmdAnimate.Enabled
cmdAccel(1).Enabled = cmdAnimate.Enabled
    
Label1.Caption = sDetails
lblFile.Caption = fileTitle
GetFileName = True
ExitRoutine:

bDlgActive = False

End Function

Private Sub PropertyPage_Initialize()
With ucGIF1
    .AutoSize = False
    .BackColor = vbButtonFace
    .BorderStyle = bdrRaised
    .Visible = False
End With
End Sub

Private Sub PropertyPage_Terminate()
ucGIF1.UnloadGIF True
End Sub

Private Sub ucGIF1_AnimationLoopComplete(ByVal loopsRemaining As Long)
lblLoops.Caption = "Loops Left: " & loopsRemaining
End Sub

Private Sub ucGIF1_AnimationLoopExpired()
Call cmdAnimate_Click
End Sub

Public Function ShowBrowser(Optional rtnFileTitle As String) As String

Dim ofn As OPENFILENAME

Dim rtnVal As Long, EndStr As Integer

With ofn
    .lStructSize = Len(ofn)
    .hwndOwner = hWnd
    .hInstance = App.hInstance
    .lpstrFilter = "GIF Files Only" & Chr$(0) & "*.gif" & Chr$(0) & Chr$(0)
    .lpstrFile = String$(255, Chr$(0))
    .nMaxFile = 255
    .lpstrFileTitle = String$(255, Chr$(0))
    .nMaxFileTitle = 255
    '.lpstrInitialDir = App.Path
    .lpstrTitle = "Select GIF File"
    .flags = 4096
End With

rtnVal = GetOpenFileName(ofn)

If rtnVal <> 0 Then
    EndStr = InStr(ofn.lpstrFile, Chr$(0))
    If EndStr Then
        ShowBrowser = Left(ofn.lpstrFile, EndStr - 1)
        EndStr = InStr(ofn.lpstrFileTitle, Chr$(0))
        If EndStr Then rtnFileTitle = Left$(ofn.lpstrFileTitle, EndStr - 1)
    End If
Else
    ShowBrowser = ""
End If

End Function


Private Sub ucGIF1_AnimationProgress(ByVal currentFrame As Long)
lblFrames.Caption = "Frame #: " & currentFrame
End Sub
